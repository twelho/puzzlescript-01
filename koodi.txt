title My Game
author My Name Here
homepage www.puzzlescript.net

(realtime_interval specifies the "speed" of gravity (seconds per block of movement))
realtime_interval 0.1
throttle_movement
noaction

========
OBJECTS
========

Background 
lightblue 

Target 
DarkBlue    

Wall 
BROWN

CharTop
Yellow

Char
Blue

Crate 
Brown darkbrown
11111
11001
10101
10011
11111

Platform
Lightgray
00000
00000
.....
.....
.....

Ladder
lightbrown
0...0
0...0
00000
0...0
0...0

Button
Red
.....
.....
.....
.....
.000.

Spikes
Black
.....
.....
.0.0.
.0.0.
00000


(Meta-objects, no need to touch these)
Player
transparent

Fell
transparent

RedDoorClosed
Red

RedDoorOpen
Red
0....
.0...
..0..
...0.
....0

GoalFlag
Blue White
100..
10000
100..
1....
1....

=======
LEGEND
=======

CharFull = Char or CharTop
GravityAffects = Char or Crate
Collidable = Platform or Wall or Crate or CharFull or Ladder
Grabbable = Ladder

a = Player
. = Background
# = Wall
T = CharTop
P = Char
* = Crate
@ = Crate and Target
O = Target
q = Platform
b = Button
l = Ladder
s = Spikes
r = RedDoorClosed
g = GoalFlag

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target, Ladder, Button, RedDoorOpen, Spikes, GoalFlag
Char, CharTop, Platform, Wall, Crate, RedDoorClosed
Fell
Player

======
RULES     
======

(On real-time ticks update gravity for all affected objects)
down [ stationary Player ] [ GravityAffects no Fell | no Collidable ] -> [ Player ] [ DOWN GravityAffects Fell | ]
[ Fell ] -> []

(Disable moving down if the object below is not Grabbable)
up [ DOWN Player ] [ No Grabbable | Char ] -> [ Player ] [ | Char ]

(Disable moving up if not grabbing onto something)
[ UP Player ] [ No Grabbable Char ] -> [ Player ] [ Char ]

(Transfer player movement to the Character, perform rigid animation for the top)
[ moving Player ] [ Char ] -> [ Player ] [ moving Char ]
+ rigid [ moving Char | CharTop ] -> [ moving Char | moving CharTop ]

(Allow the Character and other Crates to push Crates)
[ > CharFull | Crate ] -> [ > CharFull | > Crate ]
[ > Crate | Crate ] -> [ > Crate | > Crate ]

(Spikes)
[ > CharFull | Spikes ] -> [ | Spikes]

(Red Button and Red Doors)
[Button Crate] [RedDoorClosed] -> [Button Crate] [RedDoorOpen] 
[Button no Crate] [RedDoorOpen] -> [Button no Crate] [RedDoorClosed] 

==============
WINCONDITIONS
==============

Any CharFull on GoalFlag

=======     
LEVELS
=======



#########################
#a......................#
#.....s.....*.........*.#
#.....qqlqqqqqq.........#
#..ql...l...............#
#...lT..l...............#
#...lP..l.....qqqql.#####
#...l...l.*.......l.r...#
#.b*l...l.*.b.....l.r.g.#
###########.#############
#...#######b#........####
#...#########...........#
#...#########...........#
#...#########...........#
#########################


