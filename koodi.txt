title Alien wants Flag
author Sebastian, Sami, Dennis, Benjamin
homepage www.puzzlescript.net

(realtime_interval specifies the "speed" of gravity (seconds per block of movement))
realtime_interval 0.1
throttle_movement
noaction
noundo

========
OBJECTS
========

Background
LightBlue

Target
DarkBlue

Wall
BROWN darkbrown
11111
10001
10001
10001
11111

CharTop
#4caf50 Black
.101.
.101.
.000.
..0..
00000

Char
#4caf50 #d81b60
0.0.0
0.0.0
.111.
.1.1.
.0.0.

Crate
Brown darkbrown
11111
11001
10101
10011
11111

Platform
Lightgray
00000
00000
.....
.....
.....

Ladder
lightbrown
0...0
0...0
00000
0...0
0...0

Button
Red
.....
.....
.....
.....
.000.

Spikes
Black
.....
.....
.0.0.
.0.0.
00000

TPRed1
darkred Red
.000.
01110
01110
01110
.000.

TPRed2
darkred Red
.000.
01110
01110
01110
.000.

TPRed12
transparent

TPRed22
transparent

TPBLue1
darkblue Blue
.000.
01110
01110
01110
.000.

TPBLue2
darkblue Blue
.000.
01110
01110
01110
.000.

TPBLue12
transparent

TPBlue22
transparent

RedDoorClosed
Red

RedDoorOpen
Red
0....
.0...
..0..
...0.
....0

RedDoorFrame
transparent

RedDoorClosed2
Red
0....
.0...
..0..
...0.
....0

RedDoorOpen2
Red

RedDoorFrame2
transparent

GoalFlag
Blue White
100..
10000
100..
1....
1....

GoalFlag2
Blue White
100..
10000
100..
1....
1....

(Meta-objects, no need to touch these)
Player
transparent

Fell
transparent

die
transparent

=======
LEGEND
=======

CharFull = Char or CharTop
Collidable = Platform or Wall or Crate or CharFull or RedDoorClosed or RedDoorOpen2
Grabbable = Ladder
CollidableOrGrabbable = Collidable or Grabbable
Teleports = TPRed1 or TPRed2 or TPRed12 or TPRed22 or TPBLue1 or TPBLue2 or TPBLue12 or TPBlue22

a = Player
. = Background
# = Wall
T = CharTop
P = Char
* = Crate
q = Platform
b = Button
l = Ladder
s = Spikes
r = RedDoorClosed
e = RedDoorClosed2
g = GoalFlag
ä = TPRed1
ö = TPRed2
x = TPBLue1
y = TPBLue2

=======
SOUNDS
=======

sfx0 31271903 (Door)
sfx1 19243508 (Portal ?)
sfx2 92167108 (Death)
sfx3 24436300 (Win)
sfx4 12791107 (move)
sfx5 15535107 (Button)

================
COLLISIONLAYERS
================

Background
RedDoorFrame, RedDoorFrame2, die (Invisible flags in background)
Target, Ladder, Button, RedDoorOpen, RedDoorClosed2, Spikes, GoalFlag, GoalFlag2, Teleports (Background objects)
Char, CharTop, Platform, Wall, Crate, RedDoorClosed, RedDoorOpen2 (Collidable objects)
Fell
Player

======
RULES
======
[ die ] -> restart

(On real-time ticks update gravity for all affected objects)
down [ stationary Player ] [ Char no Fell | no CollidableOrGrabbable ] -> [ Player ] [ DOWN Char Fell | ]
down [ stationary Player ] [ Crate no Fell | no Collidable ] -> [ Player ] [ DOWN Crate Fell | ]
[ Fell ] -> []

(Disable horizontal movement if the Player is in the air)
up [ horizontal Player ] [ No CollidableOrGrabbable | Char ] -> [ Player ] [ | Char ]

(Disable moving down if the object below is not Grabbable)
up [ DOWN Player ] [ No Grabbable | Char ] -> [ Player ] [ | Char ]

(Disable moving up if not grabbing onto something)
[ UP Player ] [ No Grabbable Char ] -> [ Player ] [ Char ]

(Transfer player movement to the Character, perform rigid animation for the top)
[ moving Player ] [ Char ] -> [ Player ] [ moving Char ]
+ rigid [ moving Char | CharTop ] -> [ moving Char | moving CharTop ] sfx4

(Allow the Character and other Crates to push Crates)
[ > CharFull | Crate ] -> [ > CharFull | > Crate ]
[ > Crate | Crate ] -> [ > Crate | > Crate ]

(Spikes)
[ CharFull Spikes ] -> [ CharFull die ] sfx2 message You stepped on spikes.

(Red Button and Red Doors)
[ Button no Crate ] [ RedDoorOpen ] -> [ Button no Crate ] [ RedDoorClosed ] (sulje ovi jos nappia ei painettu)
[ Button Crate ] [ RedDoorClosed ] -> [ Button Crate ] [ RedDoorFrame ] (jos ovi on kiinni ja nappi on painettu, tee suljetusta ovesta temp door)
[ Button no Crate ] [ RedDoorFrame ] -> [ Button no Crate ] [ RedDoorClosed ] (jos nappia ei ole painettu ja temp ovi, laita ovi takaisin kiinni)
[ Button Crate ] [ RedDoorFrame ] -> [ Button Crate ] [ RedDoorOpen ] sfx0 (jos ovea ei äsken laitettu kiinni, avaa ovi ja soita ääniefekti)

(Red Button and Red Doors Reversed)
[ Button no Crate ] [ RedDoorOpen2 ] -> [ Button no Crate ] [ RedDoorClosed2 ]
[ Button Crate ] [ RedDoorClosed2 ] -> [ Button Crate ] [ RedDoorFrame2 ]
[ Button no Crate ] [ RedDoorFrame2 ] -> [ Button no Crate ] [ RedDoorClosed2 ]
[ Button Crate ] [ RedDoorFrame2 ] -> [ Button Crate ] [ RedDoorOpen2 ]

(Play sound effect when Button is pressed)
[ > Crate | Button ] -> sfx5

(Falling Create kills the Character)
DOWN [ Crate | CharTop ] -> [ Crate | die ] sfx2 message You were crushed.

(TELEPORTS. Only two teleports of the same color will work)
(Red teleports)
[ TPRed1 Crate ] [ TPRed2 no Crate no CharFull ] -> [ TPRed1 ] [ TPRed22 Crate ] sfx1 (TP1 - TP2)
[ TPRed2 Crate ] [ TPRed1 no Crate no CharFull ] -> [ TPRed2 ] [ TPRed12 Crate ] sfx1 (TP2 - TP1)
(Blue Teleports)
[ TPBLue1 Crate ] [ TPBLue2 no Crate no CharFull ] -> [ TPBLue1 ] [ TPBlue22 Crate ] sfx1 (TP1 - TP2)
[ TPBLue2 Crate ] [ TPBLue1 no Crate no CharFull ] -> [ TPBLue2 ] [ TPBLue12 Crate ] sfx1 (TP2 - TP1)

(Resetoi tp takaisin toimivaksi jos laatikko on siirretty pois tp edestä)
[ TPRed12 no Crate ] -> [ TPRed1 ]
[ TPRed22 no crate ] -> [ TPRed2 ]
(sama sinisille)
[ TPBLue12 no Crate ] -> [ TPBLue1 ]
[ TPBlue22 no crate ] -> [ TPBLue2 ]

(Winning plays sound)
[ CharFull GoalFlag ] -> sfx3 [ CharFull GoalFlag2 ]
==============
WINCONDITIONS
==============

Any CharFull on GoalFlag2

=======
LEVELS
=======

message Level 1
message Put a crate on the button and reach the flag to win!

#########################
#a......................#
#.......................#
#.......................#
#.......................#
#..............g........#
#..............#l.......#
#.............##l.......#
#............##.l#......#
#.......*...##..l##.....#
#...t..lq....r..lr......#
#...p..l.b...r..lr......#
#########################
#########################
#########################

MESSAGE Well that was easy... now there are more buttons though (Level 2)

#########################
#a......................#
#.....s.....*.........*.#
#.....qqlqqqqqq.........#
#..ql...l...............#
#...lT..l...............#
#...lP..l.....qqqql.#####
#...l...l.*.......l.r...#
#.b*l...l.*.b.....l.r.g.#
###########.#############
###########b#############
#########################
#########################
#########################
#########################

message Okay... this one is a bit more tricky (Level 3)

########################################
#a...........#.........................#
#............r.........................#
#.....g......r.........................#
#....qqqlqqqqqql.......................#
#.......l......l.......................#
#......*l......l.......................#
#......*l......l.......................#
#....*x*l...ä..l.......................#
#..lqqqqq......l......lqq..............#
#..l.*.........l......l................#
#..l.**........l......l*..ö............#
#..lqqqqq.....qqqqqqlqqqqqqq...........#
#..l................l..................#
#..l..t.*...........l.......*.*.b.s....#
#..l*.p.*...........l......qqqqqqqq....#
#..lqqqqq...........l..................#
#..l................l..................#
#..l...........##########l.............#
#..l..y........#......#..l.............#
#..l...........#..*......l.............#
#..l...*....b*....qlq....l.............#
#.qqqlqqq..qqq.....l.....l.............#
#....l.........#...l*.q..l.*...........#
#b...l..ssss...#...l**ss.l.*...bbb.....#
########################################

message Ayyy you win :DDDDDDDD

########################
#a.....................#
#......................#
#......................#
#......................#
#......................#
#......................#
#......................#
#......................#
#......................#
#lrrrrrl...............#
#l.....l...............#
#l.....l..*............#
#lt..lrrrrrrs..........#
#lp*.l.................#
#rrrrl......ä.y........#
#....l.................#
#....l.................#
#....l.................#
#..ö.l.................#
#...*l..*..leleee......#
#.rrrrrrr..lel.........#
#.x........lel......g..#
#eeeeeeeeeee.l.rrrrrrrr#
#ssssssssss.bl.........#
########################
