title My Game
author My Name Here
homepage www.puzzlescript.net

(realtime_interval specifies the "speed" of gravity (seconds per block of movement))
realtime_interval 0.1
throttle_movement
noaction
========
OBJECTS
========

Background 
lightblue 

Target 
DarkBlue    

Wall 
BROWN darkbrown
11111
10001
10001
10001
11111

CharTop
#4caf50 Black
.101.
.101.
.000.
..0..
00000

Char
#4caf50 #d81b60
0.0.0
0.0.0
.111.
.1.1.
.0.0.


Crate 
Brown darkbrown
11111
11001
10101
10011
11111

Platform
Lightgray
00000
00000
.....
.....
.....

Ladder
lightbrown
0...0
0...0
00000
0...0
0...0

Button
Red
.....
.....
.....
.....
.000.

Spikes
Black
.....
.....
.0.0.
.0.0.
00000

Teleport1
darkblue Blue
.000.
01110
01110
01110
.000.

Teleport2
darkred Red
.000.
01110
01110
01110
.000.

(Meta-objects, no need to touch these)
Player
transparent

Fell
transparent

RedDoorClosed
Red

RedDoorOpen
Red
0....
.0...
..0..
...0.
....0

RedDoorFrame
transparent

GoalFlag
Blue White
100..
10000
100..
1....
1....

die
transparent
=======
LEGEND
=======

CharFull = Char or CharTop
GravityAffects = Char or Crate
Collidable = Platform or Wall or Crate or CharFull or Ladder
Grabbable = Ladder

a = Player
. = Background
# = Wall
T = CharTop
P = Char
* = Crate
q = Platform
b = Button
l = Ladder
s = Spikes
r = RedDoorClosed
g = GoalFlag
n = Teleport1
m = Teleport2

=======
SOUNDS
=======

sfx0 58045703

================
COLLISIONLAYERS
================

Background
RedDoorFrame, die
Target, Ladder, Button, RedDoorOpen, Spikes, GoalFlag, Teleport1, Teleport2
Char, CharTop, Platform, Wall, Crate, RedDoorClosed
Fell
Player

======
RULES     
======

(On real-time ticks update gravity for all affected objects)
down [ stationary Player ] [ GravityAffects no Fell | no Collidable ] -> [ Player ] [ DOWN GravityAffects Fell | ]
[ Fell ] -> []

(Disable horizontal movement if the Player is in the air)
up [ horizontal Player ] [ No Collidable | Char ] -> [ Player ] [ | Char ]

(Disable moving down if the object below is not Grabbable)
up [ DOWN Player ] [ No Grabbable | Char ] -> [ Player ] [ | Char ]

(Disable moving up if not grabbing onto something)
[ UP Player ] [ No Grabbable Char ] -> [ Player ] [ Char ]

(Transfer player movement to the Character, perform rigid animation for the top)
[ moving Player ] [ Char ] -> [ Player ] [ moving Char ]
+ rigid [ moving Char | CharTop ] -> [ moving Char | moving CharTop ]

(Allow the Character and other Crates to push Crates)
[ > CharFull | Crate ] -> [ > CharFull | > Crate ]
[ > Crate | Crate ] -> [ > Crate | > Crate ]

(Spikes)
[die] -> restart
[ CharFull Spikes ] -> message You step on spikes.
[ CharFull Spikes ] -> [CharFull die] (die, jotta message näkyy ennen restart)



(Red Button and Red Doors)
[Button no Crate] [RedDoorOpen] -> [Button no Crate] [RedDoorClosed] (sulje ovi jos nappia ei painettu)
[Button Crate] [RedDoorClosed] -> [Button Crate] [RedDoorFrame] (jos ovi on kiinni ja nappi on painettu, tee suljetusta ovesta temp door)
[Button no Crate] [RedDoorFrame] -> [Button no Crate] [RedDoorClosed] (jos nappia ei ole painettu ja temp ovi, laita ovi takaisin kiinni)
[Button Crate] [RedDoorFrame] -> [Button Crate] [RedDoorOpen] sfx0 (jos ovea ei äsken laitettu kiinni, avaa ovi ja soita ääniefekti)

(Falling box kills the player)
[die] ->  restart
DOWN [Crate | CharTop] -> [Crate | die ] message You were crushed.

(Teleports. Kun haluaa uuden, pitää ylempänä tehdä uusi Teleport-pari ja sitten kopoida sille alla oleva rivi ja vaihtaa oikeat numerot)
[ Teleport1 Crate ] [ Teleport2 ] -> [ Teleport1 ] [ Teleport2 Crate ] (Teleport1 -> Teleport2)

==============
WINCONDITIONS
==============

Any CharFull on GoalFlag

=======     
LEVELS
=======

message level 1
message Press the button and reach the flag to win!


#########################
#a......................#
#.......................#
#.......................#
#.......................#
#..............g........#
#..............#l.......#
#.............##l.......#
#............##.l#......#
#.......*...##..l##.....#
#...t..lq....r..lr......#
#...p..l.b...r..lr......#
#########################
#########################
#########################


MESSAGE Well that was easy.. Now there are more buttons though(Level 2)

#########################
#a......................#
#.....s.....*.........*.#
#.....qqlqqqqqq.........#
#..ql...l...............#
#...lT..l...............#
#...lP..l.....qqqql.#####
#...l...l.*.......l.r...#
#.b*l...l.*.b.....l.r.g.#
###########.#############
###########b#############
#########################
#########################
#########################
#########################

message okay.. this one is a bit more tricky (Level 3)

########################################
#a...........#.........................#
#............r.........................#
#.....g......r.........................#
#....qqqlqqqqqql.......................#
#.......l......l.......................#
#......*l......l.......................#
#......*l......l.......................#
#......*l......l.......................#
#..lqqqqq......l......lqq..............#
#..l.*.........l......l................#
#..l.**........l......l*...............#
#..lqqqqq....qqqqqqqlqqqqqqq...........#
#..l................l..................#
#..l.*..*...........l.......*.*.b.s....#
#..l.*..*...........l......qqqqqqqq....#
#..l.qqqq...........l..................#
#..l................l..................#
#..l...........##########l.............#
#..lq..........#......#..l.............#
#..l..t........#..*......l.............#
#..l..p*n...b*....qlq....l.............#
#.qqqlqqq..qqq.....l.....l.............#
#....l.........#...l*.q..l.*...........#
#b.m.l..ssss...#...l**ss.l.*...bbb.....#
########################################

message Ayyy you win :DDDDDDDD
